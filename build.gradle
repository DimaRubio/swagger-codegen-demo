plugins {
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id 'java'
    id 'maven-publish'
    id 'org.hidetake.swagger.generator' version '2.9.0'
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

dependencies {
    swaggerCodegen 'io.swagger:swagger-codegen-cli:2.4.0-20180512.022651-254'
    compile 'com.petstore:swagger-codegen-petstore-api-client:0.0.1-SNAPSHOT'
    compile group: 'io.rest-assured', name: 'rest-assured', version: '3.1.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.4'
    compile group: 'org.threeten', name: 'threetenbp', version: '1.3.6'
    compile group: 'io.gsonfire', name: 'gson-fire', version: '1.8.3'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

swaggerSources {
    petstore {
        inputFile = file('config/api/swagger.json')
        code {
            language = 'java'
            configFile = file('config/api/swagger-codegen-config.json')
        }
    }
}

def apiClientCodePath = "build/swagger-code-petstore"

task buildPetstoreApiClientJars(type: GradleBuild) {
    dir = "$apiClientCodePath"
    tasks = ['clean', 'build']
}

publishToMavenLocal.dependsOn buildPetstoreApiClientJars

publishing {
    publications {
        petstore(MavenPublication) {
            groupId 'com.petstore'
            artifactId 'swagger-codegen-petstore-api-client'
            version '0.0.1-SNAPSHOT'

            artifact file("$apiClientCodePath/build/libs/swagger-codegen-event-api-client-0.0.1-SNAPSHOT.jar")
        }
    }
}

lombok {
    version = '1.18.0'
    sha256 = ""
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.8'
}